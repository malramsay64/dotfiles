" Malcolm Ramsay  .vimrc
"
"General {{{

if !has('nvim')
    set encoding=utf-8
endif

scriptencoding utf-8

source $HOME/dotfiles/vim/plugins.vim   " load plugins

set autoread                            " Vim automatically reads changes to a file

augroup vimrc
    autocmd!
    autocmd BufEnter * :checktime       " Reload changes on FocusGained
augroup END

filetype plugin indent on               " load filetype specific indent and plugin files

""" Python configuration
let g:loaded_python_provider = 1

if isdirectory($HOME.'/.miniconda')
    let g:python3_host_prog = $HOME.'/.miniconda/envs/neovim3/bin/python'
endif

let g:email = 'malramsay64@gmail.com'
let g:username = 'Malcolm Ramsay'

" Use existing neovim session if opening from terminal
if has('nvim') && executable('nvr')
  let $VISUAL = $HOME.'/.miniconda/envs/neovim3/bin/nvr -cc split --remote-wait'
endif

if exists('&belloff')
    set belloff=all                     " Turn off all bells
endif

"}}}
"Colours {{{

syntax enable                           " enable syntax processing

" Enable truecolour support in neovim >= 0.1.5 || vim >= 7.4.1799
if has('termguicolors')
    set termguicolors
endif

" Load colourscheme in vim with tmux
if !has('nvim')
    set t_8b=[48;2;%lu;%lu;%lum
    set t_8f=[38;2;%lu;%lu;%lum
endif

""" Configure Colourschems
let g:one_allow_italics = 1
let g:quantum_italics=1

let g:airline_theme='quantum'

set background=dark " for the dark version
"colorscheme onedark
colorscheme quantum

" Colourscheme override
highlight pythonSelf cterm=italic gui=italic
highlight Conceal guibg=NONE ctermbg=NONE
highlight Whitespace guibg=NONE ctermbg=NONE

" }}}
"Spaces and Tabs {{{

set tabstop=4           " number of visual spaces per TAB
set softtabstop=4       " number of spaces inserted upon TAB
set shiftwidth=4        " autoindent amount when using cindent
set expandtab           " TAB inserts softtabstop spaces

set autoindent          " copy indentation from previous line
set smartindent         " inserts extra indentation in some cases


" Remove trailing whitespace from file and return cursor to current position
function! StripTrailingWhitespaces()
    let l:pos = winsaveview()
    execute('%s/\s\+$//e')
    call winrestview(l:pos)
endfun

nnoremap <Leader>zz :call StripTrailingWhitespaces()<CR>


"}}}
"UI Config {{{

" The combination of relative number and number gives the absolute line
" number for the current line and relative numbers for all others
set relativenumber             " show relative line numbers
set number                     " show line numbers
set scrolloff=5                " 5 lines above and below cursor when scrolling
set wildmenu                   " visual autocomplete for command
set wildmode=longest:full,full " Complete to longest matching text then first match. Like shell completion
set lazyredraw                 " redraw only when need to
set hidden                     " allows you to hide buffers with unsaved changes without being prompted
set noshowmatch                " highlight matching parenthesis/bracket/brace
set laststatus=2               " Display statusline
set splitright                 " open splits by default to the right
set nosplitbelow               " open splits above current
set formatoptions+=n           " smart auto-indenting inside numbered lists
set formatoptions+=j           " smart comment joining
set nojoinspaces               " don't autoinsert two spaces after '.', '?', '!' for join command
set textwidth=100               " Textwidth is 100 columns

" common mistypings of exit and save
cabbrev W w
cabbrev Q q
cabbrev Wq wq
cabbrev WQ wq

" Unicode UI elements
if has('linebreak')
  let &showbreak='‚§∑ '          " ARROW POINTING DOWNWARDS THEN CURVING RIGHTWARDS (U+2937, UTF-8: E2 A4 B7)
endif
set fillchars=vert:‚îÇ           " BOX DRAWINGS LIGHT VERTICAL (U+2502, UTF-8: E2 94 82)

set list                       " we want to show whitespace elements
set listchars=tab:‚ñ∂‚Äí
set listchars+=nbsp:‚ê£
set listchars+=extends:¬ª       " RIGHT-POINTING DOUBLE ANGLE QUOTATION MARK (U+00BB, UTF-8: C2 BB)
set listchars+=precedes:¬´      " LEFT-POINTING DOUBLE ANGLE QUOTATION MARK (U+00AB, UTF-8: C2 AB)
set listchars+=trail:‚Ä¢         " BULLET (U+2022, UTF-8: E2 80 A2)

"" Movement
set backspace=eol,start,indent  " backspace deletes newlines
set whichwrap+=<,>,h,l,[,]      " left right wraps to next/previous lines

" Movement between splits
nnoremap <C-h> <C-w>h
nnoremap <C-j> <C-w>j
nnoremap <C-k> <C-w>k
nnoremap <C-l> <C-w>l

" Consistency with yank
nnoremap Y y$

"}}}
" Custom Shortcuts {{{

" Neovim terminal mappings
if has('nvim')
    tnoremap <C-h> <C-\><C-N><C-w>h
    tnoremap <C-j> <C-\><C-N><C-w>j
    tnoremap <C-k> <C-\><C-N><C-w>k
    tnoremap <C-l> <C-\><C-N><C-w>l
endif

" jk is escape
inoremap jk <esc>
inoremap jK <esc>
inoremap JK <esc>
inoremap Jk <esc>

" leader is <space>
let g:mapleader="\<space>"

" go to next split
nnoremap <Leader>w <c-w>w
" Turn off search highlighting
nnoremap <Leader>h :nohlsearch<CR>
" Reload vimrc
nnoremap <Leader>r :source $MYVIMRC<CR>
" Edit vimrc
nnoremap <Leader>er :edit $MYVIMRC<CR>
" Edit plugins
nnoremap <Leader>ep :edit $DOTFILES/vim/plugins.vim<CR>
" Edit UnltiSnips
nnoremap <Leader>es :UltiSnipsEdit<CR>
" Edit in ftplugin directory
nnoremap <Leader>ef :edit ~/.vim/ftplugin<CR>
" Edit in tmux config
nnoremap <Leader>et :edit ~/.tmux.conf<CR>
" Edit bashrc
nnoremap <Leader>eb :edit ~/.bashrc<CR>
" Edit zshrc
nnoremap <Leader>ez :edit ~/.zshrc<CR>
" Edit vim direcotry
"nnoremap <Leader>ed :edit $DOTFILES/vim/<CR>
" Edit denite config
nnoremap <Leader>ed :edit $DOTFILES/vim/denite.vim<CR>
" <C-s> will correct previous spelling misake and set undo point
nnoremap <C-s> [s1z=<c-o>
inoremap <C-s> <c-g>u<Esc>[s1z=`]A<c-g>u
" navigate between tabs
nnoremap ]w :tabnext<CR>
nnoremap [w :tabprev<CR>
" Remap for digraphs
inoremap <C-y> <C-k>

" Open last buffer
nnoremap <leader><leader> <C-^>

" Swap semicolon and colon for easier commands
nnoremap ; :
nnoremap : ;
vnoremap ; :
vnoremap : ;

" Shortcut for saving file as root
cmap w!! w !sudo tee > /dev/null %

" Faster window resizing
nnoremap <C-w>- :5wincmd -<CR>
nnoremap <C-w>= :5wincmd +<CR>
nnoremap <C-w>+ :wincmd =<CR>
nmap <leader>vj :5wincmd +<CR>
nmap <leader>vk :5wincmd -<CR>

"}}}
"Searching {{{

set incsearch " search as characters entered
set hlsearch  " highlight matches
set ignorecase " ignore case when seraching
set smartcase " overrides ignorecase if uppercase characters in search query

set magic     " regular expressions

" Use ripgrep for search if available
if executable('rg')
    set grepprg=rg\ --vimgrep\ --no-heading
    set grepformat=%f:%l:%c:%m,%f:%l:%m
endif

"}}}
"Folding {{{

set foldenable         " enable folding
set foldlevelstart=5   " have all levels open by default
set foldnestmax=2      " Maximum number of nested folds
set foldmethod=syntax  " fold based on indent

set modelines=1        " last line of file is checked to run as a command

"}}}
"Backups {{{

if exists('$SUDO_USER')
    set nobackup        " Don't write backups for root files
    set nowritebackup   " No backups when root user
    set noswapfile      " no swap files for root user

    if has('persistent_undo')
        set noundofile  " no undo for root user
    endif
else
    set backup          " Performs backups
    set writebackup     " perform backup
    set backupdir=~/.vim-tmp,~/.tmp,. " Backup directories

    set directory=~/.vim-tmp/swap//,~/.tmp/swap//,. " Directories for swap files

    if has('persistent_undo')
        set undofile
        set undodir=~/.vim-tmp/undo     "undo directory
        set undodir+=~/.tmp/undo
        set undodir+=.
    endif
endif

"}}}
"Spelling and Files{{{

set spelllang=en_au             " spelling language is english australian
set fileformats=unix,mac,dos    " default line ending is unix

" set filetype defaults
augroup filetypes
    autocmd!
    autocmd BufNewFile,BufRead *.md set ft=markdown
    if has('nvim')
        autocmd TermOpen * setlocal nonumber norelativenumber
    endif
augroup END

"}}}
" Plugin Configuration {{{
source $DOTFILES/vim/fzf.vim

" netrw {{{

let g:netrw_banner = 0
let g:netrw_liststyle = 3

" }}}
" airline {{{

let g:airline_powerline_fonts = 1

" Only check for indent and mixed-indent whitespace errors
let g:airline#extensions#whitespace#checks = [ 'indent', 'mixed-indent-file' ]

" Have a wordcount in the following files (search regex)
let g:airline#extensions#wordcount#enabled = 1
let g:airline#extensions#wordcount#filetypes = '\vhelp|vimwiki|markdown|rst|org|text|asciidoc|tex'

let g:airline#extensions#virtualenv#enabled = 1

"}}}
" Vimwiki {{{

nmap <Leader>wj <Plug>VimwikiDiaryPrevDay
nmap <Leader>wk <Plug>VimwikiDiaryNextDay

let g:vimwiki_folding = 'syntax'

" Wiki settings
let s:wiki_default = {}
let s:wiki_default.auto_export = 0
let s:wiki_default.auto_toc = 0
let s:wiki_default.nested_syntaxes = {'python': 'python', 'c++': 'cpp', 'sh': 'sh'}

let s:home_wiki = copy(s:wiki_default)
let s:home_wiki.path = '~/Documents/notes/personal/'
let s:home_wiki.diary_rel_path = 'diary/'
let s:home_wiki.path_html = '~/Documents/notes/html/personal/'

let s:phd_wiki = copy(s:wiki_default)
let s:phd_wiki.path = '~/Documents/notes/PhD/'
let s:phd_wiki.diary_rel_path = 'logbook/'
let s:phd_wiki.diary_index = 'index'
let s:phd_wiki.diary_header = 'PhD Logbook'
let s:phd_wiki.path_html = '~/Documents/notes/html/PhD/'
let s:phd_wiki.auto_export = 1
let s:phd_wiki.html_template = '~/Documents/notes/template.html'
let s:phd_wiki.template_path = '~/Documents/notes/'
let s:phd_wiki.template_default = 'template'
let s:phd_wiki.template_ext = '.html'

let s:flurosat_wiki = copy(s:wiki_default)
let s:flurosat_wiki.path = '~/Documents/notes/FluroSat/'

let g:vimwiki_list = [s:home_wiki, s:phd_wiki, s:flurosat_wiki]

" Custom image handler for html
function! VimwikiWikiIncludeHandler(value)
    let l:str = a:value

    " complete URL
    let l:url_0 = matchstr(l:str, vimwiki#vars#get_global('rxWikiInclMatchUrl'))
    " URL parts
    let l:link_infos = vimwiki#base#resolve_link(l:url_0)
    let l:descr = matchstr(l:str, vimwiki#html#incl_match_arg(1))
    let l:verbatim_str = matchstr(l:str, vimwiki#html#incl_match_arg(2))

    let l:link_infos = vimwiki#base#resolve_link(l:url_0)

    let l:url = l:link_infos.filename

    let l:url = escape(l:url, '#')

    if fnamemodify(l:url, ':e') =~? 'pdf'
        let l:line = '<figure>'.
                    \ '<object data='.l:url." type='application/pdf' height=100% width=100%></object>".
                    \'<figcaption>Fig: '.l:descr.'</figcaption>'.
                    \'</figure>'
    else
        let l:line = '<figure>'.
                    \ vimwiki#html#linkify_image(l:url, l:descr, l:verbatim_str).
                    \'<figcaption>Fig: '.l:descr.'</figcaption>'.
                    \'</figure>'
    endif

    " Return the empty string when unable to process link
    return l:line
endfunction


"}}}
" TaskWiki {{{

let g:taskwiki_sort_order='urgency+,due+,project+'

" }}}
" fugitive {{{

nnoremap <Leader>gs :Gstatus<CR>
nnoremap <Leader>gc :Gcommit<CR>

" }}}
" neomake {{{

" open loclist quickfix list if errors
let g:neomake_open_list = 0

" run neomake on write
function! MyOnBattery()
  return system("pmset -g batt | head -1 | grep 'AC Power' -c")
endfunction

if MyOnBattery()
  call neomake#configure#automake('w')
else
  call neomake#configure#automake('nw', 1000)
endif

" Change colour of warning signs
augroup neomake
    autocmd!
    autocmd BufWritePost * Neomake
    autocmd ColorScheme * hi NeomakeErrorSign ctermfg=red
    autocmd ColorScheme * hi NeomakeWarningSign ctermfg=yellow
augroup END

let g:neomake_python_enabled_makers = ['pydocstyle', 'pycodestyle', 'pylint', 'mypy']
let g:neomake_python_pycodesyle_args = ['--ignore=E123']
let g:neomake_python_pydocsyle_args = ['--ignore=D105,D203,D213,D205,D213']

" Makers for vimwiki filetype
if executable('proselint')
    let g:neomake_vimwiki_proselint_maker = {
                \ 'exe': 'proselint',
                \ 'errorformat': '%W%f:%l:%c: %m',
                \ 'postprocess': function('neomake#postprocess#GenericLengthPostprocess'),
                \}
    let g:neomake_vimwiki_enabled_makers = ['proselint']
endif

nmap <leader>md :NeomakeDisable<CR>
nmap <leader>mt :NeomakeToggle<CR>
nmap <leader>me :NeomakeEnable<CR>

" }}}
" deoplete {{{
if has('python3')
    let g:deoplete#enable_at_startup = 1
    let g:deoplete#enable_smart_case = 1

    let g:deoplete#max_list=15
    let g:deoplete#auto_complete_delay = 100

    " <C-h>: close popup
    inoremap <expr><C-h> deoplete#smart_close_popup()."\<C-h>"

    " <CR> gives newline
    inoremap <silent> <CR> <C-r>=<SID>my_cr_function()<CR>
    function! s:my_cr_function() abort
        return deoplete#close_popup() . "\<CR>"
    endfunction

    " Enable echodoc to show function variables in command bar
    set noshowmode
    let g:echodoc_enable_at_startup = 1

    " Remove preview window
    set completeopt-=preview
    "autocmd CompleteDone * pclose!

    if !exists('g:deoplete#omni_patterns')
        let g:deoplete#omni_patterns = {}
    endif
    let g:deoplete#omni_patterns.tex =
                \ '\v\\%('
                \ . '\a*cite\a*%(\s*\[[^]]*\]){0,2}\s*\{[^}]*'
                \ . '|\a*ref%(\s*\{[^}]*|range\s*\{[^,}]*%(}\{)?)'
                \ . '|hyperref\s*\[[^]]*'
                \ . '|includegraphics\*?%(\s*\[[^]]*\]){0,2}\s*\{[^}]*'
                \ . '|%(include%(only)?|input)\s*\{[^}]*'
                \ . ')\m'

endif

"}}}
" clang_complete {{{
let g:clang_use_library = 1

if !isdirectory("/usr/local/opt/llvm/lib")
    let g:clang_library_path='/usr/local/opt/llvm/lib'
endif

let g:clang_user_options = '-std=c+=11'
let g:clang_complete_auto = 1
let g:clang_hl_errors = 1
let g:clang_snippets = 1
let g:clang_snippets_engine = 'ultisnips'
let g:clang_close_preview = 1
let g:clang_auto_select = 1
let g:clang_omnicppcomplete_compliance = 0
let g:clang_make_default_keymappings = 0

" }}}
" ultisnips {{{

let g:UltiSnipsExpandTrigger='<C-k>'
let g:UltiSnipsJumpForwardTrigger='<tab>'
let g:UltiSnipsJumpBackwardTrigger='<S-tab>'

let g:UltiSnipsEditSplit='horizontal'

" Set the documentation style for the snippets
let g:ultisnips_python_style='google'

" Configuration settings
let g:snips_author = 'Malcolm Ramsay'
let g:snips_email = 'malramsay64@gmail.com'
let g:snips_github = 'https://github.com/malramsay64'

" Directories
let g:UltiSnipsSnippetsDir = '~/.vim/UltiSnips'

" }}}
" vimtex {{{

let g:tex_flavor='latex'

" enable vimtex
let g:vimtex_enabled = 1

" Folding options
let g:vimtex_fold_enabled = 1
let g:vimtex_fold_env_whitelist = ['figure', 'table']
"let g:vimtex_fold_manual = 1

" latexmk options

let g:vimtex_latexmk_enabled = 1
let g:vimtex_latexmk_build_dir = 'output'

" }}}
" vmath {{{
vmap <expr> ++ VMATH_YankAndAnalyse()
nmap ++ vip++
" }}}
" litecorrect {{{
augroup litecorrect
    autocmd!
    autocmd FileType markdown,mkd call litecorrect#init()
    autocmd FileType text call litecorrect#init()
    autocmd FileType vimwiki call litecorrect#init()
augroup END
" }}}
" incsearch {{{
set hlsearch
let g:incsearch#auto_nohlsearch = 1
map n  <Plug>(incsearch-nohl-n)
map N  <Plug>(incsearch-nohl-N)
map *  <Plug>(incsearch-nohl-*)
map #  <Plug>(incsearch-nohl-#)
map g* <Plug>(incsearch-nohl-g*)
map g# <Plug>(incsearch-nohl-g#)
" }}}
" grammarous {{{

let g:grammarous#use_vim_spelllang = 1
let g:grammarous#move_to_first_error = 0
let g:grammarous#default_lang = 'en_au'

nnoremap <leader>gg :GrammarousCheck<CR>
nnoremap <leader>gr :GrammarousReset<CR>

" }}}
" isort {{{

let g:vim_isort_map  = '<leader>i'
let g:vim_isort_python_version = 'python3'

"}}}
" jedi-vim {{{

let g:jedi#completions_command=''
let g:jedi#rename_command = '<leader>jr'
let g:jedi#usages_command = '<leader>jn'

let g:jedi#completions_enabled = 0
let g:jedi#popup_on_dot = 0

" }}}
" Goyo {{{

function! s:goyo_enter()
    silent !tmux set status off
    silent !tmux list-panes -F '\#F' | grep -q Z || tmux resize-pane -Z
    set noshowmode
    set noshowcmd
    set scrolloff=999
    call deoplete#disable()
endfunction

function! s:goyo_leave()
    silent !tmux set status on
    silent !tmux list-panes -F '\#F' | grep -q Z && tmux resize-pane -Z
    set showmode
    set showcmd
    set scrolloff=5
    call deoplete#enable()
endfunction

augroup goyo
    autocmd!
    autocmd User GoyoEnter nested call <SID>goyo_enter()
    autocmd User GoyoLeave nested call <SID>goyo_leave()
augroup END

nnoremap <leader>eg :Goyo<CR>

" }}}
" template {{{

let g:templates_directory = [$DOTFILES.'/vim/templates']

" }}}
" iron.nvim {{{

let g:iron_map_defaults = 0
let g:iron_new_python_REPL_hooks = ['import numpy as np']

nmap <leader>i <Plug>(iron-send-motion)
vmap <leader>i <Plug>(iron-send-motion)
nmap <leader>p <Plug>(iron-repeat-cmd)


" }}}
" vim-online-thesaurus {{{

let g:online_thesaurus_map_keys = 0

nnoremap <leader>y :OnlineThesaurusCurrentWord<CR>

" }}}
" undotree {{{

noremap <leader>u :UndotreeToggle<CR>

" }}}
" vim-taskwarrior {{{

" Highlighting field
let g:task_highlight_field = 0

" Long descriptions
let g:task_rc_override = 'rc.defaultwidth=0'

" Allow changes to data
let g:task_readonly = 0

" Change default fields
let g:task_default_prompt = ['description', 'project', 'tags', 'priority', 'due']

"let g:task_gui_term = 0

" }}}
"  vimterm {{{

nnoremap <leader>vo :call vimterm#toggle() <CR>
tnoremap <leader>vo <C-\><C-n>:call vimterm#toggle() <CR>

"  }}}
"  Scalpel {{{

nmap <Leader>s <Plug>(Scalpel)

"  }}}
" Tagbar {{{

nnoremap <Leader>t :TagbarToggle<CR>

" }}}

"  }}}
"
" vim:foldmethod=marker:foldlevel=0:filetype=vim
