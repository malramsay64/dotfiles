#
#
# Credit: https://kev.inburke.com/kevin/profiling-zsh-startup-time/
PROFILE_STARTUP=false
if [[ "$PROFILE_STARTUP" == true ]]; then
    zmodload zsh/zprof # Output load-time statistics
    # http://zsh.sourceforge.net/Doc/Release/Prompt-Expansion.html
    PS4=$'%D{%M%S%.} %N:%i> '
    exec 2>"${XDG_CACHE_HOME:-$HOME/tmp}/zsh_statup.$$"
    setopt xtrace prompt_subst
fi

zstyle ':completion:*' completer _expand _complete _ignored _correct _approximate
zstyle ':completion:*' max-errors 2
zstyle ':completion:*' menu select
# zstyle :compinstall filename "$HOME/.zshrc"
zstyle ':completion::complete:*' use-cache 1

# 0 -- vanilla completion (abc => abc)
# 1 -- smart case completion (abc => Abc)
# 2 -- word flex completion (abc => A-big-Car)
# 3 -- full flex completion (abc => ABraCadabra)
zstyle ':completion:*' matcher-list '' \
  'm:{a-z\-}={A-Z\_}' \
  'r:[^[:alpha:]]||[[:alpha:]]=** r:|=* m:{a-z\-}={A-Z\_}' \
  'r:|?=** m:{a-z\-}={A-Z\_}'


HISTFILE=~/.histfile
HISTSIZE=10000
SAVEHIST=10000
setopt appendhistory sharehistory incappendhistory autocd extendedglob
unsetopt beep
bindkey -v

# Use async for autosuggestions
ZSH_AUTOSUGGEST_USE_ASYNC=True

# Use histdb timer to time each command
autoload -Uz add-zsh-hook
add-zsh-hook precmd  histdb-update-outcome
# bash style word delimiter for <C>-w
autoload -U select-word-style
select-word-style bash

# Souce system wide configuration
source /etc/profile
export DOTFILES="$HOME/dotfiles"
export _Z_DATA="$HOME/.z/z"
# Source zsh plugins
# This needs to be a standard source for some reason
source "$DOTFILES/zsh/sourceables.sh"
# this includes the source_file function

function source_file() {
    if [ -f "$1" ]; then
        source $1
    fi
}
# Aliases
source_file "$DOTFILES/zsh/aliases.zsh"

# set TERM appropriately
if [[ "$TERM" == "tmux-256color" ]] && [[ $(tmux -V | awk "{print $2}") < 2 ]]; then
    export TERM="screen-256color"
fi

# Turn off system colours as they have a habit of ruining things
export LS_COLORS=

# Add tab completion for SSH hostnames based on ~/.ssh/config, ignoring wildcards
if [[ -f $HOME/.ssh/config ]]; then
  hosts=($(egrep '^Host.*' $HOME/.ssh/config | awk '{print $2}' | grep -v '^*' | sed -e 's/\.*\*$//'))
  zstyle ':completion:*:hosts' hosts $hosts
fi

# Use histdb timer to time each command
autoload -Uz add-zsh-hook
add-zsh-hook precmd  histdb-update-outcome

# Use histdb for zsh autosuggestions, with the top result being directory dependent
_zsh_autosuggest_strategy_histdb_top_here() {
    local query="select commands.argv from
history left join commands on history.command_id = commands.rowid
left join places on history.place_id = places.rowid
where places.dir LIKE '$(sql_escape $PWD)%'
and commands.argv LIKE '$(sql_escape $1)%'
group by commands.argv order by count(*) desc limit 1"
    suggestion=$(_histdb_query "$query")
}

ZSH_AUTOSUGGEST_STRATEGY=histdb_top_here

# setup prompt
autoload -U promptinit; promptinit
# Prompt is sourced so no need for `prompt pure`
# This also means that only a single prompt can be loaded at once

# Time before prompt displays execution time
PURE_CMD_MAX_EXEC_TIME=10
# The default symbol doesn't work with Iosevka
# PURE_PROMPT_SYMBOL=âŒª
PURE_GIT_UNTRACKED_DIRTY=0

# Equivalent of set backspace=indent,eol,start in vim
bindkey "^?" backward-delete-char

# fzf configuratioin
source_file "$HOME/.fzf.zsh"
if type rg > /dev/null; then
    # Use ripgrep for fzf search
    export FZF_DEFAULT_COMMAND='rg --files --no-ignore --hidden --follow -g "!{.git,node_modules}/*" 2> /dev/null'
    # Use ripgrep for ctrl-T search
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
fi

# Use neovim-remote when in a neovim buffer
if [ -n "$NVIM_LISTEN_ADDRESS" ]; then
    alias_function "nvim" "nvr"
fi

# Have consistent location for forwarded ssh authentication socket
if [[ "$SSH_AUTH_SOCK" == /tmp/* ]]; then
    ln -sf "$SSH_AUTH_SOCK" "$HOME/.ssh/ssh-agent-sock"
    export SSH_AUTH_SOCK="$HOME/.ssh/ssh-agent-sock"
elif [[ "$SSH_AUTH_SOCK" == /run/* ]]; then
    ln -sf "$SSH_AUTH_SOCK" "$HOME/.ssh/ssh-agent-sock"
    export SSH_AUTH_SOCK="$HOME/.ssh/ssh-agent-sock"
fi

# update PATH for google cloud SDK
source_file "$HOME/.local/google-cloud-sdk/path.zsh.inc"
# enable shell completion for gcloud
# source_file "$HOME/.local/google-cloud-sdk/completion.zsh.inc"
# Source local changes
source_file "$HOME/.bashrc.local"
# Source travis config
source_file "$HOME/.travis/travis.sh"
# Source conda configuration
source_file "$HOME/.miniconda/etc/profile.d/conda.sh"

# Source pyenv if present
if type pyenv >/dev/null; then
    eval "$(pyenv init - --no-rehash)"
    eval "$(pyenv virtualenv-init -)"
fi

# Source direnv if present
if type direnv >/dev/null; then
    eval "$(direnv hook zsh)"
fi

# Abbreviations
#
# These are expanded in the shell
abbr ravp='rsync -av --progress'
abbr rex='rsync -rltDvP'
abbr g='git'
abbr gap='git add -p'
abbr gst='git status'

# Cause I can never spell install correctly
abbr isntall='install'

# PBS aliases
if type qstat >/dev/null; then
    abbr qs='qstat -Jt -u $USER'
    abbr qsu='qstat -u $USER'
    abbr qdelall='qdel $(qstat -u $USER | grep ^[0-9] | cut -d. -f1 | tr "\n" " ")'
    abbr qsi='qsub -I -P PRJ-CrysGrowth'
fi

# Directory abreviations
abbr cm="~/Projects/Crystal_Melting"
abbr ss="~/Projects/statdyn-simulation"
abbr sa="~/Projects/statdyn-analysis"
abbr th="~/Projects/phd-thesis"
abbr dot="~/dotfiles"

# Add local completions to search path
fpath+="$DOTFILES/zsh/completions"

# Load Completions
autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit -i
else
  compinit -C -i
fi
unset updated_at

if [[ "$PROFILE_STARTUP" == true ]]; then
    zprof
    unsetopt xtrace
    exec 2>&-
fi

[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh
